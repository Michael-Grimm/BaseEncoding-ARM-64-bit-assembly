.text

//Aliases of registers
//Input/Output from/to calling C functions:
io0      .req x0
base0    .req x0
buffer0  .req x0
result   .req x0
value0   .req x0

io1      .req x1
base1    .req x1
buffer1  .req x1

base2    .req x2
buffer2  .req x2



//Intermediate values in routines:
asciichar  .req x4
wasciichar .req w4
alphabet   .req x5
buffer     .req x6
count      .req x7
product    .req x8
wproduct   .req w8
quotient   .req x9
radix      .req x10
rest       .req x11
restw      .req w11
value      .req x12
temp      .req x13
wtemp      .req w13
temp2      .req x14



/**
 * A macro-pair that reduces the boilerplate of function declaration
 * Creates a global function that preserves the frame pointer and procedure 
 * link register. After FUNCTION there must allways follow an END.
 */
.macro FUNCTION function_name
 	.type \function_name, %function
    .global \function_name
	\function_name:
		stp x29, x30, [sp, #-16]!
.endm

.macro END function_name
		ldp x29, x30, [sp], #16
        ret
        .size \function_name, (. - \function_name) 
.endm
 
/**
 * The macro ASCIIZ_LENGTH 
 * determines the length of a zero-terminated character array
 * The result is in register 'count'
 */
.macro ASCIIZ_LENGTH chararray
    mov buffer, \chararray
    mov count, xzr
	loopasciiz\@:
	ldrb wtemp, [buffer], #1
    and  wtemp, wtemp, #0xff
    cbz  wtemp, endasciizlength\@ //terminating zero?
    add count, count, #1
    b loopasciiz\@
    endasciizlength\@:
.endm


/**
 * int asciiz_length(char *array)
 * The function asciiz_length
 * determines the length of a zero terminated char-array.
 * Input: address of char-array
 * Returns the length of the string excluding the terminating zero.
 */
FUNCTION asciiz_length
    ASCIIZ_LENGTH buffer0
    mov result, count
END asciiz_length


/**
 * Macros and functions for converting a long long integer into an
 * ASCII-string for a given base (radix)
 */


/**
 * Macro COUNT_DIGITS 
 * calculates the number of ASCII-digits
 * necessary for representing a long long integer in a given base.
 * Input: long long integer in register 'integer', base in register 'base'
 * result in count
 */
.macro COUNT_DIGITS integer base
        mov value, \integer
	    mov count, xzr 
		loop_dec\@:
		add count, count, #1
		udiv value, value, \base
	    cbnz value, loop_dec\@
.endm




/**
 * int count_decimals(unsigned long long val);
 *  result                          value0
 * Returns the number of decimal characters for val.
 * Example: 0xFFFF = 65535 in decimal => 5 chars.
 * 0xabbafefedeadcafe = 12374483296764939006 => 20 chars.
 */
FUNCTION count_decimals
        mov radix, #10
	    COUNT_DIGITS value0 radix
        mov result, count
END count_decimals



/**
 * int count_chars(unsigned long long val, int base);
    result                value0             base1
 * Returns the number of necessary ascii characters for representing val in the 
 * given base (radix).
 * Examples: 0xFFFF = 65535 in decimal => 5 chars.
             0x177  = 567   in octal   => 3 chars
 *           0xabbafefedeadcafe = 12374483296764939006 => 20 chars.
 */
FUNCTION count_chars
	    COUNT_DIGITS value0 base1
        mov result, count
END count_chars



.section .rodata
/**
	Base 85 alphabet (RFC 1924) for encoding byte values into ASCII chars.
*/
base85alphabet: .ascii "0123456789ABCDEFGHIJKLMNOPQRSTUV" //32 bytes
                .ascii "WXYZabcdefghijklmnopqrstuvwxyz!#" //32 bytes
                .ascii "$%&()*+-;<=>?@^_`{|}~"            //21 bytes
                .skip 11, 0                               //+11 bytes => 32 bytes
.text



/**
 * Macro ENCODE
 * encodes a long long value into an ASCIIZ-String from the alphabet of 
 * the given base.
 * Expects the value in register 'value0', the base in register 'base1 and
 * the buffer for the resulting characters in register 'buffer2'
 * Result in register 'result'
 */
.macro ENCODE
       adr alphabet, base85alphabet 
       COUNT_DIGITS value0 base1 
       add buffer, buffer2, count    
       conversionloop\@:
       udiv quotient, value0, base1 
       mul  product, quotient, base1 
       sub  rest, value0, product    
       ldrb restw, [alphabet, restw, uxtw] //convert e.g. rest=11 => rest='b'
       strb restw, [buffer, #-1]!
       mov  value0, quotient     //quotient is new 'value'
       cbnz value0, conversionloop\@ 
       mov result, count
.endm 


/**
 * int encode(long long value, int base, char *buf);
 * writes the value as ascii string in the given base (radix) into buf.
 * base can have a value between 2 and 85. The result is undefined if base < 2 
 * or base > 85
 * buf must have a proper length to accommodate the ascii-string (and a 
 * terminating zero if needed):
 * For the maximum of long long integer:
 * Base 2: 64 bytes  "111111111111111111111111111111111111111111111111111111111111"
 * Base 8: 22 bytes  "1777777777777777777777"
 * Base 10: 20 bytes "18446744073709551615"
 * Base 16: 16 bytes "FFFFFFFFFFFFFFFF"
 * Base 32 ext hex: 13 bytes "FVVVVVVVVVVVV"
 * Base 85: 10 bytes "_sw2=@*|O0" 
 * See also: int count_chars(long long val, int base);
 * This function uses the Base 85 alphabet (RFC 1924) which includes the 
 * base32hex (base 32 extended hex) alphabet (RFC 2938):   
 * 0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz!#$%&()*+-;<=>?@^_`{|}~
 * Returns the number of characters written to buf.
 */
FUNCTION encode
       ENCODE
END encode

/**
 * int encode_dec(long long value, char *buf);
 writes the value as decimal string into buf.
 buf should have a length of 21 bytes (20 for the max-value of u64 plus 1 f√ºr zero-termination.)
 */
FUNCTION encode_dec 
       mov buffer2, buffer1
       mov base1, #10  
       ENCODE
END encode_dec


/**
 * int encode_hex(long long value, char *buf);
 * writes the value as hexadecimal string into buf.
 * see: int encode(long long value, int base, char *buf);
 */
FUNCTION encode_hex 
       mov buffer2, buffer1
       mov base1, #16  
       ENCODE
END encode_hex 




.section .rodata

/**
Dictionary for decoding ASCII chars to the numerical values of base 85.
                Values 
    0123456789   0- 9
    ABCDEFGHIJ  10-19
    KLMNOPQRST  20-29
    UVWXYZabcd  30-39
    efghijklmn  40-49 
    opqrstuvwx  50-59
    yz!#$%&()*  60-69
    +-;<=>?@^_  70-79
    `{|}~       80-84
Examples:
	'A' (dec 65) -> 10
	'$' (dec 36) -> 64
	'=' (dec 61) -> 74
*/               
ascii85toByte:                                          // Indices  
.space 32, -1 //32 not printable ASCII control characters   0-31   
//         !  "  #  $  %  &  '  (  )  *  +  ,  -  .  /     32-47
.byte  -1,62,-1,63,64,65,66,-1,67,68,69,70,-1,71,-1,-1      

//      0  1  2  3  4  5  6  7  8  9  :  ;  <  =  >  ?     48-63    
.byte   0, 1, 2, 3, 4, 5, 6, 7, 8, 9,-1,72,73,74,75,76  

//      @  A  B  C  D  E  F  G  H  I  J  K  L  M  N  O     64-79
.byte   77,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24

//      P  Q  R  S  T  U  V  W  X  Y  Z  [  \  ]  ^  _     80-95
.byte   25,26,27,28,29,30,31,32,33,34,35,-1,-1,-1,78,79

//       `  a  b  c  d  e  f  g  h  i  j  k  l  m  n  o    96-111
.byte   80,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50

//       p  q  r  s  t  u  v  w  x  y  z  {  |  }  ~ DEL  112-127
.byte   51,52,53,54,55,56,57,58,59,60,61,81,82,83,84,-1   //128 bytes
//     
.skip 128, -1                                             //256 bytes 
      
.text

/**
 * Macro DECODE
 * decodes an ASCIIZ-String with characters from the base 85 alphabet 
 * into a long long integer. 
 * Expects the address of the  string buffer in register 'buffer0'
 * the base in register 'base1'.
 * Result in register 'result'. A negative value indicates an empty string or a 
 * wrong ASCII char which is not in the base85 alphabet.
 */
.macro DECODE
       ASCIIZ_LENGTH buffer0 
       cbz count, invalid_value\@   //goto end if count is 0
       adr alphabet, ascii85toByte 
       mov temp, #1   //used for multiplication with the base
       mov value, xzr  //accumulator for the numerical value
       add buffer, buffer0, count  

       conversionloop\@:
		   cbz count, end_decode\@         
		   ldrb wasciichar, [buffer, #-1]! //ascii char from end of buffer
		   cbz wasciichar, end_decode\@    //goto end if ascii char is NUL
		   ldrb wasciichar, [alphabet, wasciichar, uxtw] //num value of ascii char
		   cmp wasciichar, 0xff //wrong value if == 0xff (-1) 
		   beq invalid_value\@ 
		   cmp asciichar, base1 //wrong value of character if >= base
		   bge invalid_value\@                  
		   mul  product, asciichar, temp 
		   add  value, value, product
		   mul temp, temp, base1
		   sub count, count, #1
       b conversionloop\@ 

       b end_decode\@
       invalid_value\@:
       mov value, xzr
       mvn value, value  
       end_decode\@:
       mov result, value
.endm 

/**
 * long long decode(char *asciiz, int base);
 * asciiz: pointer to a character buffer with zero termination.
 * base: a value between 2 and 85.
 * Returns the decoded value or -1 if asciiz is empty or contains invalid characters
 */
FUNCTION decode
       DECODE
END decode






